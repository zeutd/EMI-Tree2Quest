plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    // compile against the JEI API but do not include it at runtime
    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
    // at runtime, use the full JEI jar for Fabric
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")


    modApi("dev.ftb.mods:ftb-library-fabric:${rootProject.ftb_library_version}") { transitive false }
    modApi("dev.ftb.mods:ftb-quests-fabric:${rootProject.ftb_quests_version}")
    //modApi("dev.ftb.mods:ftb-chunks-fabric:${rootProject.ftb_chunks_version}")
    //modApi("dev.ftb.mods:ftb-ranks-fabric:${rootProject.ftb_ranks_version}")
    //modApi("dev.ftb.mods:ftb-essentials-fabric:${rootProject.ftb_essentials_version}")
    modApi("dev.ftb.mods:ftb-teams-fabric:${rootProject.ftb_teams_version}")

}

processResources {
    var inputs = [
            version: project.version,
            emi_version: rootProject.emi_version,
            ftb_quests_version: rootProject.ftb_quests_version
    ]

    filesMatching('fabric.mod.json') {
        expand inputs
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
