plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "emi_tree2quest.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modApi("dev.ftb.mods:ftb-library-forge:${rootProject.ftb_library_version}") { transitive false }
    modApi("dev.ftb.mods:ftb-quests-forge:${rootProject.ftb_quests_version}")
    //modApi("dev.ftb.mods:ftb-chunks-forge:${rootProject.ftb_chunks_version}")
    //modApi("dev.ftb.mods:ftb-ranks-forge:${rootProject.ftb_ranks_version}")
    //modApi("dev.ftb.mods:ftb-essentials-forge:${rootProject.ftb_essentials_version}")
    modApi("dev.ftb.mods:ftb-teams-forge:${rootProject.ftb_teams_version}")

    modCompileOnly "dev.emi:emi-forge:${emi_version}:api"
    modLocalRuntime "dev.emi:emi-forge:${emi_version}"

    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:0.2.0"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.2.0"))
}

processResources {
    var inputs = [
            version: project.version,
            emi_version: rootProject.emi_version,
            ftb_quests_version: rootProject.ftb_quests_version
    ]

    filesMatching('META-INF/mods.toml') {
        expand inputs
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}